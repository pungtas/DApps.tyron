(*
  PST ZRC: profit-sharing token Zilliqa reference contract - Proxy smart contract.
  Tyron Self-Sovereign Identity Protocol.
  Copyright (C) Tyron Pungtas and its affiliates.

  This program is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
*)

scilla_version 0

import BoolUtils IntUtils

library PSTproxy

  let one_msg = fun( msg: Message ) =>
    let nil_msg = Nil{ Message } in Cons{ Message } msg nil_msg

  type Error =
    | CodeWrongCaller

  let make_error = fun( error: Error ) =>
    let result = match error with
    | CodeWrongCaller            => Int32 -1
    end in { _exception: "Error"; code: result }

  let zero = Uint128 0

  let option_value = tfun 'A => fun( default: 'A ) => fun( input: Option 'A ) =>
    match input with
    | Some v => v
    | None => default
    end

  let option_uint128_value =
    let f = @option_value Uint128 in f zero

contract PSTproxy(
  initAdmin: ByStr20,
  name: String,
  symbol: String,
  decimals: Uint32,
  initSupply: Uint128,
  initImplementation: ByStr20
  )
  with
    let string_is_not_empty = fun( s : String ) =>
      let zero = Uint32 0 in
      let s_length = builtin strlen s in
      let s_empty = builtin eq s_length zero in
      negb s_empty
    in let name_ok = string_is_not_empty name
    in let symbol_ok = string_is_not_empty symbol
    in let name_symbol_ok = andb name_ok symbol_ok
    in let decimals_ok =
      let six = Uint32 6
      in let eighteen = Uint32 18
      in let decimals_at_least_6 = uint32_le six decimals
      in let decimals_no_more_than_18 = uint32_le decimals eighteen
      in andb decimals_at_least_6 decimals_no_more_than_18
      in andb name_symbol_ok decimals_ok
  =>
  field admin: ByStr20 = initAdmin
  field balances: Map ByStr20 Uint128 = let emp_map = Emp ByStr20 Uint128 in builtin put emp_map initAdmin initSupply
  field total_supply: Uint128 = initSupply
  field allowances: Map ByStr20 ( Map ByStr20 Uint128 ) = Emp ByStr20 ( Map ByStr20 Uint128 )
  field implementation: ByStr20 = initImplementation

procedure ThrowError(
  err: Error
  )
  e = make_error err; throw e
end

procedure VerifyCaller(
  addr: ByStr20
  )
  verified = builtin eq addr _sender;
  match verified with
  | True =>
  | False => err= CodeWrongCaller; ThrowError err
  end
end

(* ZRC-5 add native funds ($ZIL) to the contract's _balance *)
transition AddFunds()
  accept
end

transition UpgradeImplementation(
  newImplementation: ByStr20
  )
  current_admin <- admin; VerifyCaller current_admin;
  implementation := newImplementation;
  e = {
    _eventname: "ImplementationUpgraded";
    newImplementation: newImplementation
  }; event e
end

transition UpdateProxyAdmin(
  newAdmin: ByStr20
  )
  current_admin <- admin; VerifyCaller current_admin;
  admin := newAdmin;
  e = {
    _eventname: "AdminUpdated";
    to: newAdmin
  }; event e
end

transition UpdateImplementationAdmin(
  newAdmin: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "UpdateImplementationAdmin";
    _recipient: current_impl;
    _amount: zero;
    newAdmin: newAdmin;
    initiator: _sender
  } in one_msg m; send msg
end

transition UpdatePauser(
  newPauser: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "UpdatePauser";
    _recipient: current_impl;
    _amount: zero;
    newPauser: newPauser;
    initiator: _sender
  } in one_msg m; send msg
end

transition Pause()
  current_impl <- implementation;
  msg = let m = {
    _tag: "Pause";
    _recipient: current_impl;
    _amount: zero;
    initiator: _sender
  } in one_msg m; send msg
end

transition Unpause()
  current_impl <- implementation;
  msg = let m = {
    _tag: "Unpause";
    _recipient: current_impl;
    _amount: zero;
    initiator: _sender
  } in one_msg m; send msg
end

transition UpdateBlacklister(
  newBlacklister: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "UpdateBlacklister";
    _recipient: current_impl;
    _amount: zero;
    newBlacklister: newBlacklister;
    initiator: _sender
  } in one_msg m; send msg
end

transition Blacklist(
  addr: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "Blacklist";
    _recipient: current_impl;
    _amount: zero;
    addr: addr;
    initiator: _sender
  } in one_msg m; send msg
end

transition Unblacklist(
  addr: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "Unblacklist";
    _recipient: current_impl;
    _amount: zero;
    addr: addr;
    initiator: _sender
  } in one_msg m; send msg
end

transition UpdateMasterMinter(
  newMasterMinter: ByStr20
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "UpdateMasterMinter";
    _recipient: current_impl;
    _amount: zero;
    initiator: _sender;
    newMasterMinter: newMasterMinter
  } in one_msg m; send msg
end

transition IncreaseMinterAllowance(
  minter: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "IncreaseMinterAllowance";
    _recipient: current_impl;
    _amount: zero;
    minter: minter;
    amount: amount;
    initiator: _sender
  } in one_msg m; send msg
end

transition DecreaseMinterAllowance(
  minter: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  msg = let m = {
    _tag: "DecreaseMinterAllowance";
    _recipient: current_impl;
    _amount: zero;
    minter: minter;
    amount: amount;
    initiator: _sender
  } in one_msg m; send msg
end

transition Mint(
  to: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  current_supply <- total_supply;
  get_to_bal <- balances[to]; to_bal = option_uint128_value get_to_bal;
  msg = let m = {
    _tag: "Mint";
    _recipient: current_impl;
    _amount: zero;
    to: to;
    amount: amount;
    toBal: to_bal;
    currentSupply: current_supply;
    initiator: _sender
  } in one_msg m; send msg
end

transition MintCallBack(
  to: ByStr20,
  newToBal: Uint128,
  newSupply: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  balances[to] := newToBal;
  total_supply := newSupply
end

transition Burn(
  amount: Uint128
  )
  current_impl <- implementation;
  current_supply <- total_supply;
  get_burn_bal <- balances[_sender]; burn_bal = option_uint128_value get_burn_bal;
  msg = let m = {
    _tag: "Burn";
    _recipient: current_impl;
    _amount: zero;
    amount: amount;
    initiator: _sender;
    initiatorBal: burn_bal;
    currentSupply: current_supply
  } in one_msg m; send msg
end

transition BurnCallBack(
  initiator: ByStr20,
  newMinterBalance: Uint128,
  newSupply: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  balances[initiator] := newMinterBalance;
  total_supply := newSupply
end

transition LawEnforcementWipingBurn(
  addr: ByStr20
  )
  current_impl <- implementation;
  current_supply <- total_supply;
  get_bal <- balances[addr]; bal = option_uint128_value get_bal;
  msg = let m = {
    _tag: "LawEnforcementWipingBurn";
    _recipient: current_impl;
    _amount: zero;
    addr: addr;
    addrBal: bal;
    currentSupply: current_supply;
    initiator: _sender
  } in one_msg m; send msg
end

transition LawEnforcementWipingBurnCallBack(
  addr: ByStr20,
  newSupply: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  balances[addr] := zero;
  total_supply := newSupply
end

transition IncreaseAllowance(
  spender: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  get_allowance <- allowances[_sender][spender]; allowance = option_uint128_value get_allowance;
  msg = let m = {
    _tag: "IncreaseAllowance";
    _recipient: current_impl;
    _amount: zero;
    spender: spender;
    amount: amount;
    currentAllowance: allowance;
    initiator: _sender
  } in one_msg m; send msg
end

transition DecreaseAllowance(
  spender: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  get_allowance <- allowances[_sender][spender]; allowance = option_uint128_value get_allowance;
  msg = let m = {
    _tag: "DecreaseAllowance";
    _recipient: current_impl;
    _amount: zero;
    spender: spender;
    amount: amount;
    initiator: _sender;
    currentAllowance: allowance
  } in one_msg m; send msg
end

transition AllowanceCallBack(
  initiator: ByStr20,
  spender: ByStr20,
  newAllowance: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  allowances[initiator][spender] := newAllowance
end

transition Transfer(
  to: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  get_from_bal <- balances[_sender]; from_bal = option_uint128_value get_from_bal;
  get_to_bal <- balances[to]; to_bal = option_uint128_value get_to_bal;
  msg = let m = {
    _tag: "Transfer";
    _recipient: current_impl;
    _amount: zero;
    to: to;
    amount: amount;
    initiator: _sender;
    fromBal: from_bal;
    toBal: to_bal
  } in one_msg m; send msg
end

transition TransferCallBack(
  from: ByStr20,
  newFromBal: Uint128,
  to: ByStr20,
  newToBal: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  balances[from] := newFromBal;
  balances[to] := newToBal
end

transition TransferFrom(
  from: ByStr20, 
  to: ByStr20,
  amount: Uint128
  )
  current_impl <- implementation;
  get_to_bal <- balances[to];
  to_bal = option_uint128_value get_to_bal;
  get_from_bal <- balances[from]; from_bal = option_uint128_value get_from_bal;
  get_allowance <- allowances[from][_sender]; allowance = option_uint128_value get_allowance;
  msg = let m = {
    _tag: "TransferFrom";
    _recipient: current_impl;
    _amount: zero;
    from: from;
    to: to;
    amount: amount;
    initiator: _sender;
    fromBal: from_bal;
    spenderAllowance: allowance;
    toBal: to_bal
  } in one_msg m; send msg
end

transition TransferFromCallBack(
  from: ByStr20,
  to: ByStr20,
  newFromBal: Uint128,
  newToBal: Uint128
  )
  current_impl <- implementation; VerifyCaller current_impl;
  balances[from] := newFromBal;
  balances[to] := newToBal
end

transition SwapZILForPST(
  minPstAmount: Uint128
  )
  current_admin <- admin; VerifyCaller current_admin;
  current_impl <- implementation;
  balance <- _balance;
  msg = let m = {
    _tag: "SwapZILforPST";
    _recipient: current_impl;
    _amount: balance;
    minPstAmount: minPstAmount
  } in one_msg m; send msg
end

transition ProfitShare()
  current_admin <- admin; VerifyCaller current_admin;
  current_impl <- implementation;
  get_bal <- balances[current_impl]; bal = option_uint128_value get_bal
end