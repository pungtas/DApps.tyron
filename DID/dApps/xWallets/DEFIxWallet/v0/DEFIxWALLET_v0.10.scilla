(* v0.10
DEFIxWALLET: Decentralised Finance Smart Contract Wallet <> SSI Account
Self-Sovereign Identity Protocol
Copyright Tyron Mapu Community Interest Company 2023. All rights reserved.
You acknowledge and agree that Tyron Mapu Community Interest Company (Tyron) own all legal right, title and interest in and to the work, software, application, source code, documentation and any other documents in this repository (collectively, the Program), including any intellectual property rights which subsist in the Program (whether those rights happen to be registered or not, and wherever in the world those rights may exist), whether in source code or any other form.
Subject to the limited license below, you may not (and you may not permit anyone else to) distribute, publish, copy, modify, merge, combine with another program, create derivative works of, reverse engineer, decompile or otherwise attempt to extract the source code of, the Program or any part thereof, except that you may contribute to this repository.
You are granted a non-exclusive, non-transferable, non-sublicensable license to distribute, publish, copy, modify, merge, combine with another program or create derivative works of the Program (such resulting program, collectively, the Resulting Program) solely for Non-Commercial Use as long as you:
1. give prominent notice (Notice) with each copy of the Resulting Program that the Program is used in the Resulting Program and that the Program is the copyright of Tyron; and
2. subject the Resulting Program and any distribution, publication, copy, modification, merger therewith, combination with another program or derivative works thereof to the same Notice requirement and Non-Commercial Use restriction set forth herein.
Non-Commercial Use means each use as described in clauses (1)-(3) below, as reasonably determined by Tyron in its sole discretion:
1. personal use for research, personal study, private entertainment, hobby projects or amateur pursuits, in each case without any anticipated commercial application;
2. use by any charitable organization, educational institution, public research organization, public safety or health organization, environmental protection organization or government institution; or
3. the number of monthly active users of the Resulting Program across all versions thereof and platforms globally do not exceed 10,000 at any time.
You will not use any trade mark, service mark, trade name, logo of Tyron or any other company or organization in a way that is likely or intended to cause confusion about the owner or authorized user of such marks, names or logos.
If you have any questions, comments or interest in pursuing any other use cases, please reach out to us at mapu@ssiprotocol.com.*)

scilla_version 0

library DEFIxWALLET
  type Action =
    | Add
    | Remove
  
  let add = Add
  let remove = Remove
  
  let one_msg =
    fun( msg: Message ) =>
    let nil_msg = Nil{ Message } in Cons{ Message } msg nil_msg

  let zero = Uint128 0
  let zeroByStr20 = 0x0000000000000000000000000000000000000000
  let zeroByStr32 = 0x0000000000000000000000000000000000000000000000000000000000000000

  let option_value = tfun 'A => fun( default: 'A ) => fun( input: Option 'A) =>
    match input with
    | Some v => v
    | None => default end
  let option_uint128_value = let f = @option_value Uint128 in f zero
  let option_bystr20_value = let f = @option_value ByStr20 in f zeroByStr20

  let true = True
  let false = False
  let empty_string = ""
  let did = "did"
  
  type Beneficiary =
    | NftUsername of String String (* NFT Domain Name & Subdomain *)
    | Recipient of ByStr20
  
contract DEFIxWALLET(
  init_nft: ByStr32,
  init: ByStr20 with contract field dApp: ByStr20 with contract
    field dns: Map String ByStr20,
    field did_dns: Map String ByStr20 with contract
      field controller: ByStr20,
      field services: Map String ByStr20,
      field deadline: Uint128,
      field did_domain_dns: Map String ByStr20 end end end
  )
  field nft_domain: ByStr32 = init_nft
  field pending_domain: ByStr32 = zeroByStr32
  field paused: Bool = false

  (* The block number when the last transaction occurred *)
  field ledger_time: BNum = BNum 0
  
  (* A monotonically increasing number representing the amount of transactions that have taken place *)
  field tx_number: Uint128 = zero
  
  field services: Map String ByStr20 = Emp String ByStr20
  field deadline: Uint128 = Uint128 10
  field version: String = "DEFIxWALLET_0.10.0" (* @xalkan *)
  
procedure SupportTyron( tyron: Option Uint128 )
  match tyron with
  | None => | Some donation =>
    current_init <-& init.dApp;
    donateDomain = "donate"; get_addr <-& current_init.dns[donateDomain]; addr = option_bystr20_value get_addr;
    accept; msg = let m = { _tag: "AddFunds"; _recipient: addr; _amount: donation } in one_msg m; send msg end end

procedure VerifyOrigin( addr: ByStr20 )
  verified = builtin eq _origin addr; match verified with
    | True => | False => ver <- version; e = { _exception: "xWALLET-WrongCaller"; version: ver }; throw e end end

procedure RequireContractOwner( tyron: Option Uint128 )
  id <- nft_domain; current_init <-& init.dApp;
  domain = builtin to_string id;
  get_did <-& current_init.did_dns[domain]; match get_did with
  | None => ver <- version; e = { _exception: "xWALLET-DidIsNull"; version: ver }; throw e
  | Some did_ =>
      controller <-& did_.controller; VerifyOrigin controller;
      SupportTyron tyron end end

procedure Timestamp()
  current_block <- &BLOCKNUMBER; ledger_time := current_block;
  latest_tx_number <- tx_number; new_tx_number = let incrementor = Uint128 1 in builtin add latest_tx_number incrementor;
  tx_number := new_tx_number end

procedure RequireNotPaused()
  is_paused <- paused; match is_paused with
    | False => | True => ver <- version; e = { _exception: "xWALLET-WrongStatus"; version: ver }; throw e end end

procedure IsPaused()
  is_paused <- paused; match is_paused with
    | True => | False => ver <- version; e = { _exception: "xWALLET-WrongStatus"; version: ver }; throw e end end

procedure ThrowIfNullAddr( addr: ByStr20 )
  is_null = builtin eq addr zeroByStr20; match is_null with
    | False => | True => ver <- version; e = { _exception: "xWALLET-NullAddress"; version: ver }; throw e end end

procedure ThrowIfSameAddr(
  a: ByStr20,
  b: ByStr20
  )
  is_self = builtin eq a b; match is_self with
    | False => | True => ver <- version; e = { _exception: "xWALLET-SameAddress"; version: ver }; throw e end end

procedure ThrowIfSameDomain(
  a: ByStr32,
  b: ByStr32
  )
  is_same = builtin eq a b; match is_same with
    | False => | True => ver <- version; e = { _exception: "xWALLET-SameDomain"; version: ver }; throw e end end

transition UpdateDomain(
  domain: ByStr32,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron; id <- nft_domain;
  ThrowIfSameDomain id domain;
  current_init <-& init.dApp; domain_ = builtin to_string domain;
  get_did <-& current_init.did_dns[domain_]; match get_did with
    | Some did_ => pending_domain := domain
    | None => ver <- version; e = { _exception: "xWALLET-DidIsNull"; version: ver }; throw e end;
  Timestamp end

transition AcceptPendingDomain()
  RequireNotPaused; domain <- pending_domain;
  current_init <-& init.dApp; domain_ = builtin to_string domain;
  get_did <-& current_init.did_dns[domain_]; match get_did with
    | None => ver <- version; e = { _exception: "xWALLET-DidIsNull"; version: ver }; throw e
    | Some did_ =>
      controller <-& did_.controller; VerifyOrigin controller;
      nft_domain := domain; pending_domain := zeroByStr32 end;
  Timestamp end

transition Pause( tyron: Option Uint128 )
  RequireNotPaused; RequireContractOwner tyron; paused := true;
  ver <- version; e = { _eventname: "xWALLET-Paused";
    version: ver;
    pauser: _sender }; event e;
  Timestamp end

transition Unpause( tyron: Option Uint128 )
  IsPaused; RequireContractOwner tyron; paused := false;
  ver <- version; e = { _eventname: "xWALLET-Unpaused";
    version: ver;
    pauser: _sender }; event e;
  Timestamp end

transition UpdateDeadline(
  val: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron; deadline := val;
  Timestamp end

procedure FetchServiceAddr( id: String )
  current_init <-& init.dApp;
  initId = "init"; get_did <-& current_init.did_dns[initId]; match get_did with
    | None => ver <- version; e = { _exception: "xWALLET-NullInit"; version: ver }; throw e
    | Some did_ =>
      get_service <-& did_.services[id]; addr = option_bystr20_value get_service;
      services[id] := addr end end

procedure IncreaseAllowance(
  addr: ByStr20,
  spender: ByStr20,
  amount: Uint128
  )
  ThrowIfNullAddr addr;
  msg = let m = { _tag: "IncreaseAllowance"; _recipient: addr; _amount: zero;
    spender: spender;
    amount: amount } in one_msg m ; send msg end

procedure DecreaseAllowance(
  addr: ByStr20,
  spender: ByStr20,
  amount: Uint128
  )
  ThrowIfNullAddr addr;
  msg = let m = { _tag: "DecreaseAllowance"; _recipient: addr; _amount: zero;
    spender: spender;
    amount: amount } in one_msg m ; send msg end

(* Receive $ZIL native funds *)
transition AddFunds()
  RequireNotPaused; accept; Timestamp end

(* Send $ZIL to any recipient that implements the tag, e.g. "AddFunds", "", etc. *)
transition SendFunds(
  tag: String,
  beneficiary: Beneficiary,
  amount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  match beneficiary with
  | NftUsername username_ domain_ =>
    current_init <-& init.dApp;
    is_ssi = builtin eq domain_ empty_string; match is_ssi with
      | True =>
        get_addr <-& current_init.dns[username_]; addr = option_bystr20_value get_addr; ThrowIfSameAddr addr _this_address;
        msg = let m = { _tag: tag; _recipient: addr; _amount: amount } in one_msg m; send msg
      | False =>
        get_did <-& current_init.did_dns[username_]; match get_did with
          | None => ver <- version; e = { _exception: "xWALLET-DidIsNull"; version: ver }; throw e
          | Some did_ =>
            is_did = builtin eq domain_ did; match is_did with
              | True => msg = let m = { _tag: tag; _recipient: did_; _amount: amount } in one_msg m; send msg
              | False =>
                get_domain_addr <-& did_.did_domain_dns[domain_]; domain_addr = option_bystr20_value get_domain_addr;
                msg = let m = { _tag: tag; _recipient: domain_addr; _amount: amount } in one_msg m; send msg end end end
  | Recipient addr_ =>
    ThrowIfSameAddr addr_ _this_address;
    msg = let m = { _tag: tag; _recipient: addr_; _amount: amount } in one_msg m; send msg end;
  Timestamp end

transition Transfer(
  addrName: String,
  beneficiary: Beneficiary,
  amount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron; FetchServiceAddr addrName;
  get_token_addr <- services[addrName]; token_addr = option_bystr20_value get_token_addr;
  match beneficiary with
  | NftUsername username_ domain_ =>
    current_init <-& init.dApp;
    is_ssi = builtin eq domain_ empty_string; match is_ssi with
      | True =>
        get_addr <-& current_init.dns[username_]; addr = option_bystr20_value get_addr; ThrowIfSameAddr addr _this_address;
        msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
          to: addr;
          amount: amount } in one_msg m ; send msg
      | False =>
        get_did <-& current_init.did_dns[username_]; match get_did with
          | None => ver <- version; e = { _exception: "xWALLET-DidIsNull"; version: ver }; throw e
          | Some did_ =>
            is_did = builtin eq domain_ did; match is_did with
              | True =>
                msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
                to: did_;
                amount: amount } in one_msg m ; send msg
              | False =>
                get_domain_addr <-& did_.did_domain_dns[domain_]; domain_addr = option_bystr20_value get_domain_addr;
                msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
                  to: domain_addr;
                  amount: amount } in one_msg m ; send msg end end end
  | Recipient addr_ =>
    ThrowIfSameAddr addr_ _this_address;
    msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
      to: addr_;
      amount: amount } in one_msg m ; send msg end;
  Timestamp end

transition RecipientAcceptTransfer(
  sender: ByStr20,
  recipient: ByStr20,
  amount: Uint128
  ) 
  RequireNotPaused;
  is_valid = builtin eq recipient _this_address; match is_valid with
    | True => Timestamp
    | False => ver <- version; e = { _exception: "xWALLET-WrongRecipientForAcceptTransfer"; version: ver }; throw e end end 

transition RecipientAcceptTransferFrom(
  initiator: ByStr20,
  sender: ByStr20,
  recipient: ByStr20,
  amount: Uint128
  )
  RequireNotPaused;
  is_valid = builtin eq recipient _this_address; match is_valid with
    | True => Timestamp
    | False => ver <- version; e = { _exception: "xWALLET-WrongRecipientForAcceptTransferFrom"; version: ver }; throw e end end

transition TransferSuccessCallBack(
  sender: ByStr20,
  recipient: ByStr20,
  amount: Uint128
  )
  RequireNotPaused;
  is_valid = builtin eq sender _this_address; match is_valid with
    | True => | False => ver <- version; e = { _exception: "xWALLET-WrongSender"; version: ver }; throw e end end

transition TransferFromSuccessCallBack(
  initiator: ByStr20,
  sender: ByStr20,
  recipient: ByStr20,
  amount: Uint128
  )
  RequireNotPaused;
  is_valid = builtin eq initiator _this_address; match is_valid with
    | True => | False => ver <- version; e = { _exception: "xWALLET-WrongInitiator"; version: ver }; throw e end end

transition UpdateAllowance(
  action: Action,
  addrName: String,
  spender: ByStr20,
  amount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron; FetchServiceAddr addrName;
  get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  match action with
    | Add => IncreaseAllowance addr spender amount
    | Remove => DecreaseAllowance addr spender amount end;
  Timestamp end

transition AddLiquidity(
  dApp: String,
  addrName: String,
  amount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  FetchServiceAddr dApp; get_dApp <- services[dApp]; dApp_addr = option_bystr20_value get_dApp;
  FetchServiceAddr addrName; get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  IncreaseAllowance addr dApp_addr amount;
  current_block <- &BLOCKNUMBER; current_deadline <- deadline; this_deadline = builtin badd current_block current_deadline;
  accept; msg = let m = { _tag: "AddLiquidity"; _recipient: dApp_addr; _amount: _amount;
    token_address: addr;
    min_contribution_amount: amount;
    max_token_amount: amount;
    deadline_block: this_deadline } in one_msg m ; send msg;
  Timestamp end

transition RemoveLiquidity(
  dApp: String,
  addrName: String,
  amount: Uint128,
  minZilAmount: Uint128,
  minTokenAmount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  FetchServiceAddr dApp; get_dApp <- services[dApp]; dApp_addr = option_bystr20_value get_dApp;
  FetchServiceAddr addrName; get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  current_block <- &BLOCKNUMBER; current_deadline <- deadline; this_deadline = builtin badd current_block current_deadline;
  msg = let m = { _tag: "RemoveLiquidity"; _recipient: dApp_addr; _amount: zero;
    token_address: addr;
    contribution_amount: amount;
    min_zil_amount: minZilAmount;
    min_token_amount: minTokenAmount;
    deadline_block: this_deadline } in one_msg m ; send msg;
  Timestamp end

transition SwapExactZILForTokens(
  dApp: String,
  addrName: String,
  amount: Uint128,
  minTokenAmount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  FetchServiceAddr dApp; get_dApp <- services[dApp]; dApp_addr = option_bystr20_value get_dApp;
  FetchServiceAddr addrName; get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  current_block <- &BLOCKNUMBER; current_deadline <- deadline; this_deadline = builtin badd current_block current_deadline;
  accept; msg = let m = { _tag: "SwapExactZILForTokens"; _recipient: dApp_addr; _amount: amount;
    token_address: addr;
    min_token_amount: minTokenAmount;
    deadline_block: this_deadline;
    recipient_address: _this_address } in one_msg m; send msg;
  Timestamp end

transition SwapExactTokensForZIL(
  dApp: String,
  addrName: String,
  amount: Uint128,
  minZilAmount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  FetchServiceAddr dApp; get_dApp <- services[dApp]; dApp_addr = option_bystr20_value get_dApp;
  FetchServiceAddr addrName; get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  IncreaseAllowance addr dApp_addr amount;
  current_block <- &BLOCKNUMBER; current_deadline <- deadline; this_deadline = builtin badd current_block current_deadline;
  msg = let m = { _tag: "SwapExactTokensForZIL"; _recipient: dApp_addr; _amount: zero;
    token_address: addr;
    token_amount: amount;
    min_zil_amount: minZilAmount;
    deadline_block: this_deadline;
    recipient_address: _this_address } in one_msg m ; send msg;
  Timestamp end

transition SwapExactTokensForTokens(
  dApp: String,
  addrName: String,
  toAddrName: String,
  amount: Uint128,
  minTokenAmount: Uint128,
  tyron: Option Uint128
  )
  RequireNotPaused; RequireContractOwner tyron;
  FetchServiceAddr dApp; get_dApp <- services[dApp]; dApp_addr = option_bystr20_value get_dApp;
  FetchServiceAddr addrName; get_addr <- services[addrName]; addr = option_bystr20_value get_addr;
  IncreaseAllowance addr dApp_addr amount;
  FetchServiceAddr toAddrName; get_toAddr <- services[toAddrName]; toAddr = option_bystr20_value get_toAddr;
  current_block <- &BLOCKNUMBER; current_deadline <- deadline; this_deadline = builtin badd current_block current_deadline;
  msg = let m = { _tag: "SwapExactTokensForTokens"; _recipient: dApp_addr; _amount: zero;
    token0_address: addr;
    token1_address: toAddr;
    token0_amount: amount;
    min_token1_amount: minTokenAmount;
    deadline_block: this_deadline;
    recipient_address: _this_address } in one_msg m ; send msg;
  Timestamp end